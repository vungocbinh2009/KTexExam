buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'maven'
    id 'maven-publish'
    id 'java'
    id "org.jetbrains.dokka" version "1.4.32"
    id 'signing'
}

group = 'io.github.vungocbinh2009'
version = '0.3'

repositories {
    mavenCentral()
}

dependencies {
    dokkaGfmPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:1.4.32")
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'ktexexam'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'KTexExam'
                description = 'A simple library to create exam'
                url = 'https://github.com/vungocbinh2009/KTexExam'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'vungocbinh'
                        name = 'Vu Ngoc Binh'
                        email = 'vungocbinh@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/vungocbinh2009/KTexExam.git'
                    developerConnection = 'scm:git:ssh://github.com/vungocbinh2009/KTexExam.git'
                    url = 'https://github.com/vungocbinh2009/KTexExam'
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype" +
                    ".org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype" +
                    ".org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotRepo : releaseRepo
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}



